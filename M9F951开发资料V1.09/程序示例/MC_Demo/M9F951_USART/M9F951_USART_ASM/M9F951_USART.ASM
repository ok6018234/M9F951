;//*****************(ASM) COPYRIGHT 2021 Masses-Chip ****************************
;* Project Name		  : M9F951_USART.Prj
;* File Name		  : M9F951_USART.ASM
;* Author			  : MASSES CHIP
;* Version			  : V2.00
;* Date				  : 2021/09/15
;* Web				  : www.masses-chip.com
;* FAE				  : Luo
;					  : QQ：411680975
;//******************************************************************************
;
;Option:16M/16T,WDT-Enable-288ms,RST-IO,BOR 2.4V 
;说明：本USART范例每100ms发送一次数据0x55；TX0(IOF3)、RX0(IOF2),波特率为38400
;将接收到的数据格式正确的存放在R_RX_Data_Right，格式错误的存放在R_RX_Data_Error
;//******************************************************************************
;//==============================================================================
;//***********************************头文件*************************************
;//用户不可更改
;//==============================================================================
		LIST 		P=M9F951
INCLUDE	 	M9F951.INC


;//==============================================================================
;//******************************定义全局变量************************************
;//用户可以自行看需求定义变量
;//==============================================================================
userdata	udata	H'00'

R_Temp1				res		1
R_Temp2				res		1
R_RX_Data_Right		res		1		;//存放接收到格式正确的数据
R_RX_Data_Error		res		1		;//存放接收到格式错误的数据

;//==============================================================================
;//*********************************C O D E**************************************
;//==============================================================================	
		ORG			0000H
		GOTO		MainProgram
		ORG			0008H
		GOTO		Interrupt


;//==============================================================================
;//********************************程序主函数************************************
;//每100ms发送一次数据0x55
;//==============================================================================			
;//--------------------------------程序初始化------------------------------------
MainProgram:
		CALL       	SYSTEM_INIT				;//系统初始化
		CALL       	Clr_RAM_0000H_07FFH		;//清零RAM
		CALL       	USART_INIT				;//USART初始化
;//-------------------------------主函数循环-------------------------------------		
MainLoop:
		CLRWDT
		CAll		Delay_100ms
		CALL		USART_Send_Data
		GOTO		MainLoop

;//==============================================================================
;//********************************发送函数**************************************
;/发送数据
;//==============================================================================
USART_Send_Data:
		MOVIA		0x55
		MOVAR		TX0REG
		JBTS1		TX0CR,6
		GOTO		$-2
		RETURN
		
;//==============================================================================
;//******************************中断服务子程序**********************************
;//接收数据，检测数据格式是否出错，正确放入R_RX_Data_Right，错误放入R_RX_Data_Error
;//注：中断接收标志由硬件清零
;//==============================================================================
Interrupt:
		JBTS0		INTF2,RX0IF
		GOTO		USART_Recive_Data
Interrupt_END:
;//中断结束
	    RETIE		1						;//返回A、BSR、STATUS的值

;//==============================================================================
;//********************************接收函数**************************************
;/接收数据
;//==============================================================================	
USART_Recive_Data:
		JBTS0		RX0CR,1
		GOTO		Save_Data_Error
Save_Data_Right:
		MOVR		RX0REG,0
		MOVAR		R_RX_Data_Right
		GOTO		Interrupt_End
Save_Data_Error:
		MOVR		RX0REG,0
		MOVAR		R_RX_Data_Error
		GOTO		Interrupt_End
;//==============================================================================
;//*********************************USART初始化**********************************
;//端口设置：RX接收端口设为输入，TX发送端口设为输出
;//波特率设置：目标波特率=Fosc/（（BRGD+1）×n）=38400
;//开启USART接收中断和总中断，关闭发送中断
;//==============================================================================	
USART_INIT:
;//1、端口设置
		BSET		OEF,3			;//TX0
		BCLR		OEF,2			;//RX0
;//2、USART设置
		MOVIA      	0x80
		MOVAR		TX0CR
		MOVIA		0x80
		MOVAR		RX0CR
;//3、波特率设置
		MOVIA		0x00
		MOVAR		BRGD0H
		MOVIA		0x67
		MOVAR		BRGD0L			;//波特率38400
;//4、中断设置
		BCLR		INTF2,RX0IF
		BCLR		INTF2,TX0IF
		BCLR		INTCR2,TX0IE
		BSET		INTCR2,RX0IE
		BSET		OPTION,GIEH
		RETURN

;//==============================================================================
;//********************************延时函数**************************************
;//延时100ms
;//==============================================================================
Delay_100ms:
		MOVIA		167
		MOVAR		R_Temp1
D1:
		MOVIA		200
		MOVAR		R_Temp2
D2:
		DJZR		R_Temp2,1,0
		GOTO		D2
		DJZR		R_Temp1,1,0
		GOTO		D1
		RETURN

;//==============================================================================
;//**********************************系统初始化**********************************
;//系统时钟初始化：快钟、慢钟都正常工作，不进休眠
;//IO初始化:设置个IO状态为输出低，且关闭上下拉
;//中断总开关：关闭总开关
;//==============================================================================		
SYSTEM_INIT:
;//-----------------------------------------------------------------------           
;//Init Clock
		MOVIA       b'00000000'
		MOVAR		OSCM
;//Init IO	
		BANKBSR	   	0x0F		;//切页，ANSx寄存器需要切页操作
        ;//IOA
        MOVIA      	0x00 
		MOVAR		ANSA		;//0:数字口	  1:模拟口 
        MOVAR     	PUA			;//0:上拉关闭 1:上拉使能  	 	
        MOVAR      	PDA			;//0:下拉关闭 1:下拉使能
		MOVAR      	IOA        	;//0:输出低   1:输出高
		MOVIA      	0xFF    	
        MOVAR      	OEA			;//0:输入     1:输出   	
		;//IOB
        MOVIA      	0x00
		MOVAR		ANSB
        MOVAR      	PUB
        MOVAR      	PDB
        MOVAR      	IOB
		MOVIA      	0xFF
        MOVAR      	OEB
		;//IOC
        MOVIA      	0x00
		MOVAR		ANSC
        MOVAR      	PUC
        MOVAR      	PDC
        MOVAR      	IOC
		MOVIA      	0xFF
        MOVAR      	OEC
		;//IOD
        MOVIA      	0x00
		MOVAR		ANSD
        MOVAR      	PUD
        MOVAR      	PDD
        MOVAR      	IOD
		MOVIA      	0xFF
        MOVAR      	OED
		;//IOE
        MOVIA      	0x00
		MOVAR		ANSE
        MOVAR      	PUE
        MOVAR      	PDE
        MOVAR      	IOE
		MOVIA      	0xFF
        MOVAR      	OEE
		;//IOF
        MOVIA      	0x00
		MOVAR		ANSF
        MOVAR     	PUF
        MOVAR      	PDF
        MOVAR      	IOF
		MOVIA      	0xFF
        MOVAR      	OEF

;//------------------------------------------------------------------------      
;//Init Interrupt           
        BCLR       	OPTION,6		;//1:全局中断低优先级中断使能,0:屏蔽全局中断低优先级中断
		BCLR       	OPTION,7		;//1:全局中断高优先级中断使能,0:屏蔽全局中断高优先级中断
		RETURN

;//==============================================================================
;//*****************************RAM清零(0000H~07FFH)*****************************
;//RAM清零：RAM区0000H~07FFH全部清零
;//==============================================================================				
Clr_RAM_0000H_07FFH:
		CLRR    	FSR0L
		CLRR    	FSR0H
ClrRam_Loop:
		MOVIA		0x00
		MOVAR   	INDF0
		INCR    	FSR0L,1
		MOVIA		0
		ADCAR		FSR0H,1
		MOVIA		8
		SUBRA		FSR0H,0
		JBTS1		STATUS,C
		GOTO    	ClrRam_Loop
RAM_INIT_END:
		RETURN


		END
;//******************************END OF FILE************************************