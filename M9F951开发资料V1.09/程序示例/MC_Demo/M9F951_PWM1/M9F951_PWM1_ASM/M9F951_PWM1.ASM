;//*****************(ASM) COPYRIGHT 2021 Masses-Chip ****************************
;* Project Name		  : M9F951_PWM1.prj
;* File Name		  : M9F951_PWM1.ASM
;* Author			  : MASSES CHIP
;* Version			  : V2.00
;* Date				  : 2021/09/15
;* Web				  : www.masses-chip.com
;* FAE				  : Luo
;					  : QQ：411680975
;//******************************************************************************
;
;Option:16M/16T,WDT-Enable-288ms,RST-IO,BOR 2.4V 
;说明：本范例以TC1为时基产生1KHz占空比为50的PWM波形
;PWM输出口为IOB0，IOB1
;PWM波形每100ms开关一次
;//******************************************************************************
;//==============================================================================
;//***********************************头文件*************************************
;//用户不可更改
;//==============================================================================
		LIST 		P=M9F951
INCLUDE	 	M9F951.INC


;//==============================================================================
;//******************************定义全局变量************************************
;//用户可以自行看需求定义变量
;//==============================================================================
userdata	udata	H'00'

R_Temp1			RES			1
R_Temp2			RES			1

;//==============================================================================
;//*********************************C O D E**************************************
;//==============================================================================	
		ORG			0000H
		GOTO		MainProgram
		ORG			0008H
		GOTO		Interrupt	


;//==============================================================================
;//********************************程序主函数************************************
;//==============================================================================			
;//--------------------------------程序初始化------------------------------------
MainProgram:
		CALL		SYSTEM_INIT				;//系统初始化
		CALL		Clr_RAM_0000H_07FFH		;//清零RAM
		CALL		PWM1_INIT				;//PWM1初始化
;//------------------------------主函数循环--------------------------------------		
MainLoop:
		CLRWDT
		BSET		PWM100CR,6
		BSET		PWM101CR,6
		CALL		Delay_100ms
		BCLR		PWM100CR,6
		BCLR		PWM101CR,6
		CALL		Delay_100ms
		GOTO		MainLoop
		
		
;//==============================================================================
;//******************************中断服务子程序**********************************
;//==============================================================================
;//中断进来
Interrupt:
		NOP
Interrupt_END:
;//中断结束
	    RETIE		1						;//返回A、BSR、STATUS的值

;//==============================================================================
;//*********************************PWM1初始化***********************************
;//TC1设置：递增模式，16M，高频系统时钟，1:64分频	t1 = 计数/（时钟/分频）= 1ms
;//PWM1设置：高电平有效，占空比50%	   频率 = 1/t1 =1KHz
;//延时100ms：t2 = R_Temp1*R_Temp2*周期数=100ms
;//==============================================================================	
PWM1_INIT:
;//1、TC1设置
		MOVIA      	0x0E
		MOVAR	    T1CR
		CLRR		TC1CL
		CLRR	   	TC1CH
		MOVIA		249
		MOVAR		TC1PRL
		MOVIA		0
		MOVAR		TC1PRH
		BSET       	T1CR,7
;//2、PWM1设置
		BANKBSR		0x0E
		MOVIA		0x80
		MOVAR		PWM100CR
		MOVAR		PWM101CR
		MOVIA		(249+1)/2
		MOVAR		PWM100DL
		MOVAR		PWM101DL
		MOVIA		0
		MOVAR		PWM100DH
		MOVAR		PWM101DH
		RETURN

;//==============================================================================
;//*********************************延时函数*************************************
;//延时100ms：t2 = R_Temp1*R_Temp2*周期数=100ms
;//==============================================================================
Delay_100ms:
		MOVIA		167
		MOVAR		R_Temp1
D1:
		MOVIA		200
		MOVAR		R_Temp2
D2:
		DJZR		R_Temp2,1,0
		GOTO		D2
		DJZR		R_Temp1,1,0
		GOTO		D1
		RETURN

;//==============================================================================
;//**********************************系统初始化**********************************
;//系统时钟初始化：快钟、慢钟都正常工作，不进休眠
;//IO初始化:设置个IO状态为输出低，且关闭上下拉
;//中断总开关：关闭总开关
;//==============================================================================		
SYSTEM_INIT:
;//-----------------------------------------------------------------------           
;//Init Clock
		MOVIA       b'00000000'
		MOVAR		OSCM
;//Init IO	
		BANKBSR	   	0x0F		;//切页，ANSx寄存器需要切页操作
        ;//IOA
        MOVIA      	0x00 
		MOVAR		ANSA		;//0:数字口	  1:模拟口 
        MOVAR     	PUA			;//0:上拉关闭 1:上拉使能  	 	
        MOVAR      	PDA			;//0:下拉关闭 1:下拉使能
		MOVAR      	IOA        	;//0:输出低   1:输出高
		MOVIA      	0xFF    	
        MOVAR      	OEA			;//0:输入     1:输出   	
		;//IOB
        MOVIA      	0x00
		MOVAR		ANSB
        MOVAR      	PUB
        MOVAR      	PDB
        MOVAR      	IOB
		MOVIA      	0xFF
        MOVAR      	OEB
		;//IOC
        MOVIA      	0x00
		MOVAR		ANSC
        MOVAR      	PUC
        MOVAR      	PDC
        MOVAR      	IOC
		MOVIA      	0xFF
        MOVAR      	OEC
		;//IOD
        MOVIA      	0x00
		MOVAR		ANSD
        MOVAR      	PUD
        MOVAR      	PDD
        MOVAR      	IOD
		MOVIA      	0xFF
        MOVAR      	OED
		;//IOE
        MOVIA      	0x00
		MOVAR		ANSE
        MOVAR      	PUE
        MOVAR      	PDE
        MOVAR      	IOE
		MOVIA      	0xFF
        MOVAR      	OEE
		;//IOF
        MOVIA      	0x00
		MOVAR		ANSF
        MOVAR     	PUF
        MOVAR      	PDF
        MOVAR      	IOF
		MOVIA      	0xFF
        MOVAR      	OEF

;//------------------------------------------------------------------------      
;//Init Interrupt           
        BCLR       	OPTION,6		;//1:全局中断低优先级中断使能,0:屏蔽全局中断低优先级中断
		BCLR       	OPTION,7		;//1:全局中断高优先级中断使能,0:屏蔽全局中断高优先级中断
		RETURN

;//==============================================================================
;//*****************************RAM清零(0000H~07FFH)*****************************
;//RAM清零：RAM区0000H~07FFH全部清零
;//==============================================================================				
Clr_RAM_0000H_07FFH:
		CLRR    	FSR0L
		CLRR    	FSR0H
ClrRam_Loop:
		MOVIA		0x00
		MOVAR   	INDF0
		INCR    	FSR0L,1
		MOVIA		0
		ADCAR		FSR0H,1
		MOVIA		8
		SUBRA		FSR0H,0
		JBTS1		STATUS,C
		GOTO    	ClrRam_Loop
RAM_INIT_END:
		RETURN


		END
;//******************************END OF FILE************************************