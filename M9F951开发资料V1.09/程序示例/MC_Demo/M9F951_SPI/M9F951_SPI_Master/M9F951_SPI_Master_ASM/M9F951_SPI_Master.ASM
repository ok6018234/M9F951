;//*****************(ASM) COPYRIGHT 2021 Masses-Chip ****************************
;* Project Name		  : M9F951_SPI_Master.prj
;* File Name		  : M9F951_SPI_Master.ASM
;* Author			  : MASSES CHIP
;* Version			  : V2.00
;* Date				  : 2021/09/15
;* Web				  : www.masses-chip.com
;* FAE				  : Luo
;					  : QQ：411680975
;//******************************************************************************
;
;Option:16M/16T,WDT-Disable-288ms,RST-IO,BOR 2.4V 
;说明：端口SS0(IOF7)、SCK0(IOF6)、SDO0(IOF5)、SDI0(IOF4)，
;向从机发送0x55,接收从机发送的数据存放在SPI_Receive_Data
;//******************************************************************************
;//==============================================================================
;//***********************************头文件*************************************
;//用户不可更改
;//==============================================================================
		LIST 		P=M9F951
INCLUDE	 	M9F951.INC


;//==============================================================================
;//******************************定义全局变量************************************
;//用户可以自行看需求定义变量
;//==============================================================================
userdata		udata	H'00'
SPI_Receive_Data		res		1		;//数据地址
R_Temp1					RES		1
;//==============================================================================
;//*********************************C O D E**************************************
;//==============================================================================	
		ORG			0000H
 		GOTO		MainProgram
		ORG			0008H
		GOTO		Interrupt	

;//==============================================================================
;//********************************程序主函数************************************
;//==============================================================================			
;//--------------------------------程序初始化------------------------------------
MainProgram:
		CALL		SYSTEM_INIT				;//系统初始化
		CALL		Clr_RAM_0000H_07FFH		;//清零RAM
		CALL		SPI_Master_INIT			;//SPI主机初始化
;//------------------------------主函数循环--------------------------------------		
MainLoop:
		CLRWDT
		CALL		SPI_Send_Data
		CALL		Delay_1ms
		GOTO		MainLoop
		
		
;//==============================================================================
;//******************************SPI发送函数*************************************
;//主机发送0x55，接收从机发送的数据存在SPI_Receive_Data中
;//==============================================================================
SPI_Send_Data:
		BCLR		IOF,7
		MOVIA		0x55
		MOVAR   	I2C0DBUF
		JBTS1   	I2C0SR,2 
		GOTO    	$-1
		CALL		Delay_1ms
		MOVR		I2C0DBUF,0
		MOVAR		SPI_Receive_Data
		BSET		IOF,7
		RETURN
		
		
;//==============================================================================
;//******************************延时函数****************************************
;//==============================================================================	
Delay_1ms:
		MOVIA		34
		MOVAR		R_Temp1
D1:
		DJZR		R_Temp1,1
		GOTO		D1
		RETURN
;//==============================================================================
;//******************************中断服务子程序**********************************
;//==============================================================================
;//中断进来
Interrupt:
		NOP

Interrupt_END:
;//中断结束
	    RETIE		1						;//返回A、BSR、STATUS的值

;//==============================================================================
;//******************************SPI主机初始化***********************************
;//端口设置：发送口设置为输出模式，接收口设置为输入模式
;//SPI主机设置：作为主机，MSB优先，需要片选信号
;//关闭所有中断
;//==============================================================================	
SPI_Master_INIT:
;//1、端口设置
		BSET		OEF,7				;//SS0
		BSET		IOE,7
		BSET		OEF,6				;//SCK0
		BSET		OEF,5				;//SDO0
		BCLR		OEF,4				;//SDI0
		BANKBSR		0x0F
		CLRR		ANSF
;//2、SPI设置
		MOVIA		0x48
		MOVAR		I2C0CR1
		MOVIA		0x00
		MOVAR		I2C0CR2
		MOVIA		0x07
		MOVAR		I2C0PRESC
;//2、中断设置
		BCLR		INTF2,0
		BCLR		INTCR2,0
		BCLR       	OPTION,7
		RETURN
;//==============================================================================
;//**********************************系统初始化**********************************
;//系统时钟初始化：快钟、慢钟都正常工作，不进休眠
;//IO初始化:设置个IO状态为输出低，且关闭上下拉
;//中断总开关：关闭总开关
;//==============================================================================		
SYSTEM_INIT:
;//-----------------------------------------------------------------------           
;//Init Clock
		MOVIA       b'00000000'
		MOVAR		OSCM
;//Init IO	
		BANKBSR	   	0x0F		;//切页，ANSx寄存器需要切页操作
        ;//IOA
        MOVIA      	0x00 
		MOVAR		ANSA		;//0:数字口	  1:模拟口 
        MOVAR     	PUA			;//0:上拉关闭 1:上拉使能  	 	
        MOVAR      	PDA			;//0:下拉关闭 1:下拉使能
		MOVAR      	IOA        	;//0:输出低   1:输出高
		MOVIA      	0xFF    	
        MOVAR      	OEA			;//0:输入     1:输出   	
		;//IOB
        MOVIA      	0x00
		MOVAR		ANSB
        MOVAR      	PUB
        MOVAR      	PDB
        MOVAR      	IOB
		MOVIA      	0xFF
        MOVAR      	OEB
		;//IOC
        MOVIA      	0x00
		MOVAR		ANSC
        MOVAR      	PUC
        MOVAR      	PDC
        MOVAR      	IOC
		MOVIA      	0xFF
        MOVAR      	OEC
		;//IOD
        MOVIA      	0x00
		MOVAR		ANSD
        MOVAR      	PUD
        MOVAR      	PDD
        MOVAR      	IOD
		MOVIA      	0xFF
        MOVAR      	OED
		;//IOE
        MOVIA      	0x00
		MOVAR		ANSE
        MOVAR      	PUE
        MOVAR      	PDE
        MOVAR      	IOE
		MOVIA      	0xFF
        MOVAR      	OEE
		;//IOF
        MOVIA      	0x00
		MOVAR		ANSF
        MOVAR     	PUF
        MOVAR      	PDF
        MOVAR      	IOF
		MOVIA      	0xFF
        MOVAR      	OEF

;//------------------------------------------------------------------------      
;//Init Interrupt           
        BCLR       	OPTION,6		;//1:全局中断低优先级中断使能,0:屏蔽全局中断低优先级中断
		BCLR       	OPTION,7		;//1:全局中断高优先级中断使能,0:屏蔽全局中断高优先级中断
		RETURN

;//==============================================================================
;//*****************************RAM清零(0000H~07FFH)*****************************
;//RAM清零：RAM区0000H~07FFH全部清零
;//==============================================================================				
Clr_RAM_0000H_07FFH:
		CLRR    	FSR0L
		CLRR    	FSR0H
ClrRam_Loop:
		MOVIA		0x00
		MOVAR   	INDF0
		INCR    	FSR0L,1
		MOVIA		0
		ADCAR		FSR0H,1
		MOVIA		8
		SUBRA		FSR0H,0
		JBTS1		STATUS,C
		GOTO    	ClrRam_Loop
RAM_INIT_END:
		RETURN


		END
;//******************************END OF FILE************************************