;//*****************(ASM) COPYRIGHT 2021 Masses-Chip ****************************
;* Project Name		  : M9F951_SPI_Slave.prj
;* File Name		  : M9F951_SPI_Slave.ASM
;* Author			  : MASSES CHIP
;* Version			  : V2.00
;* Date				  : 2021/09/15
;* Web				  : www.masses-chip.com
;* FAE				  : Luo
;					  : QQ：411680975
;//******************************************************************************
;
;Option:16M/16T,WDT-Disable-288ms,RST-IO,BOR 2.4V 
;说明：端口SS0(IOF7)、SCK0(IOF6)、SDO0(IOF5)、DI0(IOF4)，
;向主机发送数据0xAA，接收主机发送的数据存在Receive_Data
;//******************************************************************************
;//==============================================================================
;//***********************************头文件*************************************
;//用户不可更改
;//==============================================================================
		LIST 		P=M9F951
INCLUDE	 	M9F951.INC


;//==============================================================================
;//******************************定义全局变量************************************
;//用户可以自行看需求定义变量
;//==============================================================================
userdata	udata		H'00'
Receive_Data		res		1

;//==============================================================================
;//*********************************C O D E**************************************
;//==============================================================================	
		ORG			0000H
		GOTO		MainProgram
		ORG			0008H
		GOTO		Interrupt


;//==============================================================================
;//********************************程序主函数************************************
;//==============================================================================			
;//--------------------------------程序初始化------------------------------------
MainProgram:
		CALL		SYSTEM_INIT				;//系统初始化
		CALL		Clr_RAM_0000H_07FFH		;//清零RAM
		CALL		SPI_Slave_Init			;//SPI从机初始化
;//------------------------------主函数循环--------------------------------------		
MainLoop:
		CLRWDT
		GOTO		MainLoop
		
		
;//==============================================================================
;//******************************中断服务子程序**********************************
;//接收的数据存放在Receive_Data，发送0xAA
;//==============================================================================
;//中断进来
Interrupt:
		JBTS1		INTF2,0
		GOTO		Interrupt_END
		MOVIA		0xAA
		MOVAR		I2C0DBUF
		JBTS1		I2C0SR,2
		GOTO		$-1	
		MOVR		I2C0DBUF,0
		MOVAR		Receive_Data
Interrupt_END:
;//中断结束
	    RETIE		1						;//返回A、BSR、STATUS的值

;//==============================================================================
;//*******************************SPI从机初始化**********************************
;//端口设置：发送口设置为输出模式，接收口设置为输入模式
;//启用SPI通讯，片选信号来自于SSx端口
;//开启I2C/SPI中断和总中断
;//==============================================================================	
SPI_Slave_Init:
;//1、端口设置
		BCLR		OEF,7				;//SS0
		BCLR		OEF,6				;//SCK0
		BSET		OEF,5				;//SDO0
		BCLR		OEF,4				;//SDI0
		MOVIA		0x20
		MOVAR		OEF
		BANKBSR		0x0F
		CLRR		ANSF
;//2、SPI设置
		MOVIA      	0x40
		MOVAR	    I2C0CR1
		MOVIA		0x40
		MOVAR		I2C0CR2
		BSET       	I2C0PRESC,0
		BSET       	I2C0PRESC,1
		BSET       	I2C0PRESC,2
;//3、中断设置
		BCLR		INTF2,0
		BSET		INTCR2,0
		BSET       	OPTION,7
		RETURN
;//==============================================================================
;//**********************************系统初始化**********************************
;//系统时钟初始化：快钟、慢钟都正常工作，不进休眠
;//IO初始化:设置个IO状态为输出低，且关闭上下拉
;//中断总开关：关闭总开关
;//==============================================================================		
SYSTEM_INIT:
;//-----------------------------------------------------------------------           
;//Init Clock
		MOVIA       b'00000000'
		MOVAR		OSCM
;//Init IO	
		BANKBSR	   	0x0F		;//切页，ANSx寄存器需要切页操作
        ;//IOA
        MOVIA      	0x00 
		MOVAR		ANSA		;//0:数字口	  1:模拟口 
        MOVAR     	PUA			;//0:上拉关闭 1:上拉使能  	 	
        MOVAR      	PDA			;//0:下拉关闭 1:下拉使能
		MOVAR      	IOA        	;//0:输出低   1:输出高
		MOVIA      	0xFF    	
        MOVAR      	OEA			;//0:输入     1:输出   	
		;//IOB
        MOVIA      	0x00
		MOVAR		ANSB
        MOVAR      	PUB
        MOVAR      	PDB
        MOVAR      	IOB
		MOVIA      	0xFF
        MOVAR      	OEB
		;//IOC
        MOVIA      	0x00
		MOVAR		ANSC
        MOVAR      	PUC
        MOVAR      	PDC
        MOVAR      	IOC
		MOVIA      	0xFF
        MOVAR      	OEC
		;//IOD
        MOVIA      	0x00
		MOVAR		ANSD
        MOVAR      	PUD
        MOVAR      	PDD
        MOVAR      	IOD
		MOVIA      	0xFF
        MOVAR      	OED
		;//IOE
        MOVIA      	0x00
		MOVAR		ANSE
        MOVAR      	PUE
        MOVAR      	PDE
        MOVAR      	IOE
		MOVIA      	0xFF
        MOVAR      	OEE
		;//IOF
        MOVIA      	0x00
		MOVAR		ANSF
        MOVAR     	PUF
        MOVAR      	PDF
        MOVAR      	IOF
		MOVIA      	0xFF
        MOVAR      	OEF

;//------------------------------------------------------------------------      
;//Init Interrupt           
        BCLR       	OPTION,6		;//1:全局中断低优先级中断使能,0:屏蔽全局中断低优先级中断
		BCLR       	OPTION,7		;//1:全局中断高优先级中断使能,0:屏蔽全局中断高优先级中断
		RETURN

;//==============================================================================
;//*****************************RAM清零(0000H~07FFH)*****************************
;//RAM清零：RAM区0000H~07FFH全部清零
;//==============================================================================				
Clr_RAM_0000H_07FFH:
		CLRR    	FSR0L
		CLRR    	FSR0H
ClrRam_Loop:
		MOVIA		0x00
		MOVAR   	INDF0
		INCR    	FSR0L,1
		MOVIA		0
		ADCAR		FSR0H,1
		MOVIA		8
		SUBRA		FSR0H,0
		JBTS1		STATUS,C
		GOTO    	ClrRam_Loop
RAM_INIT_END:
		RETURN


		END
;//******************************END OF FILE************************************