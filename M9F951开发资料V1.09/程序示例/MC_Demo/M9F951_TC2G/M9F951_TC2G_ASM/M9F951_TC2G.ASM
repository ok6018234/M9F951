;//*****************(ASM) COPYRIGHT 2021 Masses-Chip ****************************
;* Project Name		  : M9F951_TC2G.Prj
;* File Name		  : M9F951_TC2G.ASM
;* Author			  : MASSES CHIP
;* Version			  : V2.00
;* Date				  : 2021/09/15
;* Web				  : www.masses-chip.com
;* FAE				  : Luo
;					  : QQ：411680975
;//******************************************************************************
;
;Option:16M/16T,WDT-Enable-288ms,RST-IO,BOR 2.4V 
;说明：本范例是TC2以TC0(1ms定时)门控模式为上升沿到上升沿
;	   当TC0第二次上升沿TC2G产生中断(2ms)并翻转IOB0口
;//******************************************************************************
;//==============================================================================
;//***********************************头文件*************************************
;//用户不可更改
;//==============================================================================
		LIST 		P=M9F951
INCLUDE	 	M9F951.INC


;//==============================================================================
;//******************************定义全局变量************************************
;//用户可以自行看需求定义变量
;//==============================================================================
userdata	udata	H'00'

R_Flag		res    1
#define		b_TC2G_F		R_Flag,0

;//==============================================================================
;//*********************************C O D E**************************************
;//==============================================================================	
		ORG			0000H
		GOTO		MainProgram
		ORG			0008H
		GOTO		Interrupt


;//==============================================================================
;//********************************程序主函数************************************
;//==============================================================================			
;//--------------------------------程序初始化------------------------------------
MainProgram:
		CALL       	SYSTEM_INIT				;//系统初始化
		CALL       	Clr_RAM_0000H_07FFH		;//清零RAM
		CALL       	TC2G_INIT				;//TC2G初始化
;//------------------------------主函数循环--------------------------------------		
MainLoop:
		CLRWDT
		JBTS1		b_TC2G_F
		GOTO		MainLoop
		BCLR		b_TC2G_F
		BNEG		IOB,0
		GOTO		MainLoop


;//==============================================================================
;//******************************中断服务子程序**********************************
;//门控定时2ms，进一次中断，翻转一次IOB0口
;//==============================================================================
;//中断进来
Interrupt:
		JBTS1		INTF0,TC2GIF
		GOTO		Interrupt_END
		BCLR		INTF0,TC2GIF
		BSET		b_TC2G_F
		BSET		TC2GCR,6
Interrupt_END:
;//中断结束
	    RETIE		1						;//返回A、BSR、STATUS的值

;//==============================================================================
;//*********************************TC2G初始化***********************************
;//TC0设置：8模式，16M，高频系统时钟，1:64分频		t1=(249+1)/(16/64)=1ms
;//TC2设置：递增模式，16M，高频系统时钟，1:64分频	t2=(249+1)/(16/64)=1ms
;//TC2G设置：TC2门控源TC0溢出周期，门控模式上升沿到上升沿	t=t1+t2=1ms+1ms=2ms
;//开启TC2G溢出中断和总中断
;//==============================================================================	
TC2G_INIT:
;//1、门控源TC0设置
		MOVIA      	0x0E
		MOVAR	    T0CR
		CLRR	   	TC0CH
		MOVIA		249
		MOVAR		TC0CL
;//2、TC2设置
		MOVIA      	0x0E
		MOVAR	    T2CR
		CLRR		TC2CL
		CLRR	   	TC2CH
		MOVIA      	249
		MOVAR	   	TC2PRL
		MOVIA		0
		MOVAR		TC2PRH
;//3、TC2G门控设置
		MOVIA		0x8E
		MOVAR		TC2GCR
		BSET		T0CR,7
		BSET       	T2CR,7
		BSET		TC2GCR,6
;//4、中断设置
		BCLR		INTF0,TC2GIF
		BSET		INTCR0,TC2GIE
		BSET       	OPTION,7
		RETURN


;//==============================================================================
;//**********************************系统初始化**********************************
;//系统时钟初始化：快钟、慢钟都正常工作，不进休眠
;//IO初始化:设置个IO状态为输出低，且关闭上下拉
;//中断总开关：关闭总开关
;//==============================================================================		
SYSTEM_INIT:
;//-----------------------------------------------------------------------           
;//Init Clock
		MOVIA       b'00000000'
		MOVAR		OSCM
;//Init IO
		BANKBSR	   	0x0F		;//切页，ANSx寄存器需要切页操作
        ;//IOA
        MOVIA      	0x00
		MOVAR		ANSA		;//0:数字口	  1:模拟口 
        MOVAR     	PUA			;//0:上拉关闭 1:上拉使能  	 	
        MOVAR      	PDA			;//0:下拉关闭 1:下拉使能
		MOVAR      	IOA        	;//0:输出低   1:输出高
		MOVIA      	0xFF
        MOVAR      	OEA			;//0:输入     1:输出   	
		;//IOB
        MOVIA      	0x00
		MOVAR		ANSB
        MOVAR      	PUB
        MOVAR      	PDB
        MOVAR      	IOB
		MOVIA      	0xFF
        MOVAR      	OEB
		;//IOC
        MOVIA      	0x00
		MOVAR		ANSC
        MOVAR      	PUC
        MOVAR      	PDC
        MOVAR      	IOC
		MOVIA      	0xFF
        MOVAR      	OEC
		;//IOD
        MOVIA      	0x00
		MOVAR		ANSD
        MOVAR      	PUD
        MOVAR      	PDD
        MOVAR      	IOD
		MOVIA      	0xFF
        MOVAR      	OED
		;//IOE
        MOVIA      	0x00
		MOVAR		ANSE
        MOVAR      	PUE
        MOVAR      	PDE
        MOVAR      	IOE
		MOVIA      	0xFF
        MOVAR      	OEE
		;//IOF
        MOVIA      	0x00
		MOVAR		ANSF
        MOVAR     	PUF
        MOVAR      	PDF
        MOVAR      	IOF
		MOVIA      	0xFF
        MOVAR      	OEF

;//------------------------------------------------------------------------      
;//Init Interrupt
        BCLR       	OPTION,6		;//1:全局中断低优先级中断使能,0:屏蔽全局中断低优先级中断
		BCLR       	OPTION,7		;//1:全局中断高优先级中断使能,0:屏蔽全局中断高优先级中断
		RETURN
		
;//==============================================================================
;//*****************************RAM清零(0000H~07FFH)*****************************
;//RAM清零：RAM区0000H~08FFH全部清零
;//==============================================================================				
Clr_RAM_0000H_07FFH:
		CLRR    	FSR0L
		CLRR    	FSR0H
ClrRam_Loop:
		MOVIA		0x00
		MOVAR   	INDF0
		INCR    	FSR0L,1
		MOVIA		0
		ADCAR		FSR0H,1
		MOVIA		8
		SUBRA		FSR0H,0
		JBTS1		STATUS,C
		GOTO    	ClrRam_Loop
RAM_INIT_END:
		RETURN


		END
;//******************************END OF FILE************************************